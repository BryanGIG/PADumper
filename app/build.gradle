plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.kotlin.plugin.compose'
    id 'kotlin-parcelize'
    id 'org.lsposed.lsplugin.jgit'
}

var repo = jgit.repo(true)
var commitCount = (repo?.commitCount("refs/remotes/origin/master") ?: 1)

android {
    compileSdk 34
    namespace "com.dumper.android"

    defaultConfig {
        applicationId  "com.dumper.android"
        minSdk 21
        targetSdk 34
        versionCode commitCount
        versionName "1.0.3"
    }

    signingConfigs {
        debug {
            storeFile file("keystore.jks")
            keyAlias "PADumper"
            storePassword "012345"
            keyPassword "012345"
        }
        release {
            storeFile file("keystore.jks")
            keyAlias "PADumper"
            storePassword "012345"
            keyPassword "012345"
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            signingConfig signingConfigs.debug
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    buildFeatures {
        aidl true
        buildConfig true
        viewBinding true
        compose true
    }

    kotlinOptions {
        jvmTarget = '17'
    }
}

composeCompiler {
    enableStrongSkippingMode = true
    reportsDestination = layout.buildDirectory.dir("compose_compiler")
}

dependencies {

    //Ui
    def composeBom = platform("androidx.compose:compose-bom:2024.10.01")
    implementation composeBom
    androidTestImplementation composeBom

    implementation 'androidx.compose.material3:material3'
    implementation 'androidx.compose.material:material-icons-extended'

    // Android Studio Preview support
    implementation 'androidx.compose.ui:ui-tooling-preview'
    debugImplementation 'androidx.compose.ui:ui-tooling'
    // Integration with activities
    implementation 'androidx.activity:activity-compose:1.9.3'
    // Integration with ViewModels
    implementation "androidx.lifecycle:lifecycle-viewmodel-compose:2.8.7"
    implementation "androidx.compose.runtime:runtime-livedata:1.7.5"
    // Integration with Navigation
    implementation "androidx.navigation:navigation-compose:2.8.3"
    implementation 'com.google.android.material:material:1.12.0'

    //Root
    def libsuVersion = "6.0.0"
    implementation "com.github.topjohnwu.libsu:core:$libsuVersion"
    implementation "com.github.topjohnwu.libsu:service:$libsuVersion"

    //Storage Helper
    implementation "com.anggrayudi:storage:2.0.0"
}
